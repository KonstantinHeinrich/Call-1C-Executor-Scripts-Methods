
метод Скрипт()
    знч ПутьСкрипта = "F:\\1C\\Executor\\SingularPluralRus.sbsl" //1
    Консоль.Записать("I have " + ВызватьМетодСкрипта(ПутьСкрипта, "СклонениеСЧислом", [4, "apple", "-"])) //2
    знч Параметры = {"Количество": 6, "Единственное": "orange", "Множественное": "-"} //3
    Консоль.Записать("You have " + ВызватьМетодСкрипта(ПутьСкрипта, "СклонениеСЧислом", Параметры))
    Консоль.Записать("He has " + ВызватьМетодСкрипта(ПутьСкрипта, "СклонениеСЧислом", 25, "cherry", "cherries"))
;

метод ВызватьМетодСкрипта(ПутьСкрипта: Строка, ИмяМетода = "", Параметры = []): Строка //1

    знч ПутьИсполнителя = "F:\\1C\\Executor\\bin\\executor_j11.cmd" //2
    
    знч Аргументы = новый Массив() //3
    Аргументы.ДобавитьВсе(["-s", "\"%ПутьСкрипта\""]) //4
    если не ИмяМетода.Пусто() //5
        Аргументы.ДобавитьВсе(["-m", ИмяМетода])
    ;
    Аргументы.ДобавитьВсе(Параметры) //6

    пер Результат = ""
    знч Процесс = новый ПроцессОс(ПутьИсполнителя, Аргументы) //7
    Процесс.Запустить()
    пока Истина
        знч Вывод = Процесс.ПотокВывода.ПрочитатьКакТекст() //8
        если Вывод.Пусто()
            прервать
        ;
        Результат += "\н%Вывод" //9
    ;

    возврат Результат.Сократить() //10
;

метод ВызватьМетодСкрипта(ПутьСкрипта: Строка, ИмяМетода = "", Параметры = {:}): Строка //1
    
    знч МассивПарам = новый Массив()
    для Парам из Параметры //2
        МассивПарам.Добавить(Парам.Значение)
    ;
    возврат ВызватьМетодСкрипта(ПутьСкрипта, ИмяМетода, МассивПарам) //3
;

метод ВызватьМетодСкрипта(
    ПутьСкрипта: Строка,
    ИмяМетода = "",
    Параметр1: Строка|Число|Булево|? = Неопределено, //1
    Параметр2: Строка|Число|Булево|? = Неопределено,
    Параметр3: Строка|Число|Булево|? = Неопределено): Строка
    
    знч МассивПарам = новый Массив()
    если Параметр1 != Неопределено
        МассивПарам.Добавить(Параметр1)
    ;
    если Параметр2 != Неопределено
        МассивПарам.Добавить(Параметр2)
    ;
    если Параметр3 != Неопределено
        МассивПарам.Добавить(Параметр3)
    ;
    возврат ВызватьМетодСкрипта(ПутьСкрипта, ИмяМетода, МассивПарам)
;
